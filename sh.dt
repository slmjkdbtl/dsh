$ term = import("term")
$ sys = import("sys")

term.no_echo()
sys.signal(~ () {})

@ {

	-- prompt
	$ home = sys.getenv("HOME")
	$ user = sys.getenv("USER")
	$ host = "broccoli"
	$ cwd = sys.cwd():replace(home, "~")
	sys.print("\n")
	sys.print(term.dim(user + "@" + host))
	sys.print("\n")
	sys.print(term.bold(term.magenta(cwd)))
	sys.print("\n")
	sys.print(term.yellow("> "))

	$ line = ""
	$ cur = 0

	~ prev_word () {
		? (cur > 0) {
			$ pos = line:rfind_at(" ", cur - 2)
			? (pos != -1) cur = pos + 1 | cur = 0
		}
	}

	~ next_word () {
		$ pos = line:find_at(" ", cur + 2)
		? (pos != -1) cur = pos - 1 | cur = #line
	}

	~ del_word () {
		$ pos = line:rfind_at(" ", cur - 2)
		? (pos != -1) {
			line = line[0..(pos + 1)] + line[cur..#line]
			cur = pos + 1
		} | {
			line = line[cur..#line]
			cur = 0
		}
	}

	~ del () {
		? (cur > 0) {
			line = line[0..(cur - 1)] + line[cur..#line]
			cur -= 1
		}
	}

	~ insert (ch) {
		line = line[0..cur] + ch + line[cur..#line]
		cur += 1
	}

	~ move_left() ? (cur > 0) cur -= 1
	~ move_right() ? (cur < #line) cur += 1
	~ move_up() { --- todo --- }
	~ move_down() { --- todo --- }

	~ escaped () {

		$ ch = sys.getchar()

		? (ch == "[") {
			ch = sys.getchar()
			? (ch == "A") move_up()
			| (ch == "B") move_down()
			| (ch == "C") move_right()
			| (ch == "D") move_left()
		}
		| (ch == "b") prev_word()
		| (ch == "f") next_word()
		| (ch:code() == 127) del_word()

	}

	-- line edit
	@ {

		$ ch = sys.getchar()

		? (ch:code() == 127) del()
		| (ch:code() == 27) escaped()
		| (ch == "\n") { sys.print("\n") @> }
		| insert(ch)

		-- render
		sys.print(term.clear_line())
		sys.print(term.move_cursor_col(1))
		sys.print(term.yellow("> "))
		sys.print(line)
		sys.print(term.move_cursor_col(cur + 3))

	}

	-- exec
	$ chunks = line:split(" ")
	$ cmd = chunks[0]

	? (cmd == "cd") { ? (#chunks > 1) sys.chdir(chunks[1]) }
	| (cmd == "exit") sys.exit()
	| sys.exec(line)

}
